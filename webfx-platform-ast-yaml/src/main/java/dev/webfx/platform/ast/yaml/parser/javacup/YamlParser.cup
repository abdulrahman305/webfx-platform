package dev.webfx.platform.ast.yaml.parser.javacup;

import dev.webfx.platform.util.ast.AST;
import dev.webfx.platform.util.ast.AstObject;
import dev.webfx.platform.util.ast.AstArray;
import dev.webfx.platform.util.Numbers;
import dev.webfx.platform.util.tuples.Pair;

/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:

    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
        System.err.println(generateErrorMessage(message, info));
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        throw new IllegalArgumentException(generateErrorMessage(message, info));
    }

    private String generateErrorMessage(String message, Object info) {

        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");

        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }

        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);

        return m.toString();
    }
:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).*/
terminal          LCURLY, RCURLY, LBRACK, RBRACK, COLON, COMMA;
terminal          NULL;
terminal Object   NUMBER, MINUS;
terminal Boolean  BOOLEAN;
terminal String   STRING, IDENTIFIER;

/* Non terminals used in the grammar section.*/
non terminal Object                     yaml_element;
non terminal Object                     yaml_native_scalar, yaml_java_scalar;
non terminal AstArray                   yaml_array;
non terminal AstArray                   yaml_array_body;
non terminal AstObject                  yaml_object;
non terminal AstObject                  yaml_object_body;
non terminal Pair<String, Object>       yaml_object_entry;

/* -------------Precedence and Associatively of Terminals Section----------- */

start with yaml_element;

/* ----------------------------Grammar Section-------------------- */

yaml_element ::= yaml_native_scalar:s
              {: RESULT = s; :}
            | yaml_object:o
              {: RESULT = o; :}
            | yaml_array:a
              {: RESULT = a; :}
            ;

yaml_native_scalar
            ::= yaml_java_scalar:s
              {: RESULT = s; :}
            ;

yaml_java_scalar
            ::= NUMBER:n
              {: RESULT = n; :}
            | MINUS NUMBER:n
              {: RESULT = Numbers.negate(n); :}
            | BOOLEAN:b
              {: RESULT = b; :}
            | NULL
              {: RESULT = null; :}
            | STRING:s
              {: RESULT = s; :}
            | IDENTIFIER:s
              {: RESULT = s; :}
            ;

yaml_array  ::= LBRACK yaml_array_body:a RBRACK
               {: RESULT = a ; :}
            | LBRACK RBRACK
               {: RESULT = AST.createArray() ; :}
            ;

yaml_array_body ::= yaml_array_body:a COMMA yaml_element:e
              {: RESULT = a.push(e); :}
            | yaml_element:e
              {: RESULT =  AST.createArray().push(e); :}
            ;

yaml_object ::= LCURLY yaml_object_body:o RCURLY
               {: RESULT = o ; :}
            | LCURLY RCURLY
               {: RESULT =  AST.createObject() ; :}
            ;

yaml_object_body ::= yaml_object_body:o COMMA yaml_object_entry:e
              {: RESULT = o.set(e.get1(), e.get2()); :}
            | yaml_object_entry:e
              {: RESULT =  AST.createObject().set(e.get1(), e.get2()); :}
            ;

yaml_object_entry ::= IDENTIFIER:id COLON yaml_element:e
               {: RESULT = new Pair(id, e) ; :}
            ;
